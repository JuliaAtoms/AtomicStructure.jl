# * Hartree‚ÄìFock equation

mutable struct HFEquation{T, Œ¶T, #<:RadialCoeff{T},
                          B<:AbstractQuasiMatrix,
                          O<:AbstractOrbital,
                          A<:Atom{T,Œ¶T,B,O},
                          E,
                          M# <:OrbitalHamiltonian{T,Œ¶T,B,O}
                          }
    atom::A
    equation::E
    orbital::O
    œï::RadialOrbital{T,B}
    hamiltonian::M
end

SCF.update!(eq::HFEquation; kwargs...) = update!(eq.hamiltonian; kwargs...)
SCF.KrylovWrapper(eq::HFEquation) = KrylovWrapper(eq.hamiltonian)

include("symbolic_hfequations.jl")

function HFEquation(atom::A, (one_body,(two_body,multipole_terms))::E,
                    orbital::O,
                    symmetry_orbitals::Vector{O}) where {T,Œ¶T, #<:RadialCoeff{T},
                                       B<:AbstractQuasiMatrix,
                                       O<:AbstractOrbital,
                                       A<:Atom{T,Œ¶T,B,O},
                                       E<:Tuple{Vector{<:OneBodyHamiltonian},Tuple{Vector{<:DirectExchangePotentials},Vector{NTuple{2,Vector{Pair{Int,Float64}}}}}}}
    R = radial_basis(atom)

    hÃÇ = one_body_hamiltonian(atom, orbital)

    korb = Ket(orbital)

    OV = typeof(view(atom, 1))
    PO = typeof(R*Diagonal(Vector{T}(undef, size(R,2)))*R')
    direct_potentials = Vector{Pair{DirectPotential{O,T,Œ¶T,B,OV,PO},T}}()
    exchange_potentials = Vector{Pair{ExchangePotential{O,T,Œ¶T,B,OV,PO},T}}()

    count(.!iszero.(one_body)) == 1 ||
        throw(ArgumentError("There can only be one one-body Hamiltonian per orbital"))

    all(AngularMomentumAlgebra.isdiagonal.(two_body)) ||
        throw(ArgumentError("Non-diagonal repulsion potentials not yet supported"))

    for (tb,mpt) ‚àà zip(two_body,multipole_terms)
        tb.o.v == orbital ||
            throw(ArgumentError("Repulsion potential $(tb) not pertaining to $(orbital)"))

        other = tb.a
        v = view(atom, other)

        for (k,c) in mpt[1]
            push!(direct_potentials,
                  HFPotential(:direct, k, other, v) => c)
        end
        for (k,c) in mpt[2]
            push!(exchange_potentials,
                  HFPotential(:exchange, k, other, v) => c)
        end
    end

    projector = Projector(OV[view(atom, other)
                             for other in symmetry_orbitals])

    hamiltonian = OrbitalSplitHamiltonian(hÃÇ, direct_potentials, exchange_potentials,
                                          projector)

    HFEquation(atom, (one_body,two_body), orbital, view(atom, orbital), hamiltonian)
end

SCF.energy(hfeq::HFEquation{E,O,M}, term::Symbol=:all) where {E,O,M} = (hfeq.œï' * hfeq.hamiltonian[term] * hfeq.œï)[1]

function Base.show(io::IO, hfeq::HFEquation)
    write(io, "Hartree‚ÄìFock equation: 0 = [ùìó  - E($(hfeq.orbital))]|$(hfeq.orbital)‚ü© = [$(hfeq.hamiltonian) - E($(hfeq.orbital))]|$(hfeq.orbital)‚ü©")
    EHa = SCF.energy(hfeq)
    write(io, "\n    ‚ü®$(hfeq.orbital)| ùìó |$(hfeq.orbital)‚ü© = $(EHa) Ha = $(27.211EHa) eV")
    Eh = (hfeq.œï' * hfeq.hamiltonian[:onebody] * hfeq.œï)[1]
    Ed = (hfeq.œï' * hfeq.hamiltonian[:direct] * hfeq.œï)[1]
    Ex = (hfeq.œï' * hfeq.hamiltonian[:exchange] * hfeq.œï)[1]
    write(io, " (‚ü®h‚ü© = $(Eh) Ha, ‚ü®J‚ü© = $(Ed) Ha, ‚ü®K‚ü© = $(Ex) Ha)")
end

# * Orbital symmetries
"""
    find_symmetries(orbitals)

Group all orbitals according to their symmetries, e.g. ‚Ñì for
`Orbital`s. This is used to determine which off-diagonal Lagrange
multipliers are necessary to maintain orthogonality.
"""
find_symmetries(orbitals::Vector{O}) where {O<:AbstractOrbital} =
    merge!(vcat, [Dict(symmetry(orb) => [orb])
                  for orb in orbitals]...)

# * Setup Hartree‚ÄìFock equations

function hf_equations(csf::NonRelativisticCSF, eng::Number; verbosity=0)
    pconfig = peel(csf.config)
    orbitals = pconfig.orbitals

    Œªs = lagrange_multipliers(orbitals)
    eng += Œªs

    if verbosity > 2
        println("Orbitals: $(orbitals)")
        println("Lagrange multipliers: $(Œªs)")
    end
    verbosity > 1 && println("E = $eng\n")

    map(pconfig) do (orb,occ,state)
        equation = diff(eng, orb, occ)
        verbosity > 2 && println("\n‚àÇ[$(orb)] E = $(equation)")
        orb => equation
    end
end

"""
    hf_equations(csf[; verbosity=0])

Derive the Hartree‚ÄìFock equations for the non-relativistic
configuration state function `csf`. All constituent orbitals are
assumed spectroscopic, i.e. they are all assigned Lagrange multipliers
to ensure their orthonormality. Additionally, orbitals of the same `‚Ñì`
but different `n` are assigned off-diagonal Lagrange multipliers.
"""
function hf_equations(csf::NonRelativisticCSF; verbosity=0)
    verbosity > 0 &&
        println("Deriving HF equations for $(csf)")

    # energy_expression has to be provided by an angular momentum
    # library.
    eng = energy_expression(csf; verbosity=verbosity-3)[2][1]
    hf_equations(csf, eng; verbosity=verbosity)
end

function hf_equations(config::Configuration{O}; verbosity=0,
                      selector::Function = peel) where {O<:SpinOrbital}
    verbosity > 0 &&
        println("Deriving HF equations for $(config)")
    h = one_body_hamiltonian_matrix(O, [config], selector=selector)[1]
    HC = two_body_hamiltonian_matrix(O, [config], selector=selector)[1]

    orbitals = selector(config).orbitals
    symmetries = find_symmetries(orbitals)

    map(orbitals) do orb
        corb = Conjugate(orb)

        ‚àÇh = filter(d -> !iszero(d), diff.(h.integrals, Ref(corb)))
        ‚àÇHC = diff.(HC.integrals, Ref(corb))
        nz = .!map(iszero, ‚àÇHC)

        multipole_terms = multipole_expand.(HC.integrals[nz])

        # Find all other orbitals of the same symmetry as the current
        # one. These will be used to create a projector, that projects
        # out their components.
        symmetry_orbitals = filter(o -> o != orb, symmetries[symmetry(orb)])

        orb,(‚àÇh, (‚àÇHC[nz], multipole_terms)),symmetry_orbitals
    end
end

"""
    diff(atom)

Differentiate the energy expression associated with the `atom`'s
CSF(s) with respect to the atomic orbitals to derive the Hartree‚ÄìFock
equations for the orbitals.
"""
function Base.diff(atom::Atom; verbosity=0)
    length(atom.configurations) > 1 &&
        throw(ArgumentError("Cannot derive Hartree‚ÄìFock equations for a multi-configurational atom"))

    map(hf_equations(atom.configurations[1]; verbosity=verbosity)) do (orb,equation,symmetry_orbitals)
        verbosity > 3 && display(equation)
        hfeq = HFEquation(atom, equation, orb, symmetry_orbitals)
        verbosity > 2 && println(hfeq)
        hfeq
    end
end

Base.view(fock::Fock{A,E}, args...) where {A<:Atom,E} =
    view(fock.quantum_system.radial_orbitals.mul.factors[2], args...)
